#include "<inc>/const.h"
#include "<inc>/rt.h"

table KConsoleDigits
	'0' '1' '2' '3' '4' '5' '6' '7' '8' '9' 'a' 'b' 'c' 'd' 'e' 'f'
endtable

fn Putx { nx -- }
	if (nx@ 15 >)
		auto a
		nx@ 16 / a!

		nx@ 16 a@ * - nx!
		a@ Putx
	end

	[nx@]KConsoleDigits@ Putc
end

fn Putn { n -- }
	if (n@ 9 >)
		auto a
		n@ 10 / a!

		n@ 10 a@ * - n!
		a@ Putn
	end

	[n@]KConsoleDigits@ Putc
end

fn Puti { n -- }
	if (n@ 0 s<)
		'-' Putc
	end

	n@ abs Putn
end

fn VPrintf { argvt argcn fmt -- }
	auto i
	0 i!

	auto sl
	fmt@ strlen sl!

	auto a
	0 a!

	auto max
	argcn@ 4 * max!
	
	while (i@ sl@ <)
		auto char
		fmt@ i@ + gb char!
		if (char@ '%' ~=)
			char@ Putc
		end else
			1 i +=
			if (i@ sl@ >=)
				return
			end

			fmt@ i@ + gb char!

			if (char@ '%' ==)
				'%' Putc
				1 i +=
				continue
			end

			if (a@ max@ >=)
				'?' Putc
				1 i +=
				continue
			end

			if (char@ 'd' ==)
				a@ argvt@ + @ Putn

				4 a +=
			end elseif (char@ 'x' ==)
				a@ argvt@ + @ Putx

				4 a +=
			end elseif (char@ 's' ==)
				a@ argvt@ + @ Puts
				
				4 a +=
			end elseif (char@ 'l' ==)
				a@ argvt@ + @ Putc

				4 a +=
			end elseif (char@ 'i' ==)
				a@ argvt@ + @ Puti

				4 a +=
			end
		end

		1 i +=
	end
end

fn Printf { ... fmt -- }
	argv argc@ fmt@ VPrintf
end